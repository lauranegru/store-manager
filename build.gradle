plugins {
    id('idea')
    id('java')
    id('jacoco')
    id('com.adarshr.test-logger') version('4.0.0')
    id('org.springframework.boot') version('3.2.5')
    id('io.spring.dependency-management') version('1.1.4')
}

sourceSets {

    unit {
        java {
            srcDirs(file('src/test/unit/java'))
        }

        resources {
            srcDirs(file("src/test/unit/resources"))
        }

        compileClasspath += main.output
        runtimeClasspath += main.output

    }

    integration {

        java {
            srcDirs(file('src/test/integration/java'))
        }

        resources {
            srcDirs(file('src/test/integration/resources'))
        }

        compileClasspath += main.output
        runtimeClasspath += main.output

    }

    acceptance {

        java {
            srcDirs(file('src/test/acceptance/java'))
        }

        resources {
            srcDirs(file('src/test/acceptance/resources'))
        }

        compileClasspath += main.output
        runtimeClasspath += main.output

    }

    test {
        java {
            srcDirs()
        }
    }

}

configurations {

    unitImplementation {
        extendsFrom(testImplementation)
    }

    unitRuntimeOnly {
        extendsFrom(testRuntimeOnly)
    }

    unitCompileOnly {
        extendsFrom(testCompileOnly)
    }

    integrationImplementation {
        extendsFrom(testImplementation)
    }

    integrationRuntimeOnly {
        extendsFrom(testRuntimeOnly)
    }

    integrationCompileOnly {
        extendsFrom(testCompileOnly)
    }

    acceptanceImplementation {
        extendsFrom(testImplementation)
    }

    acceptanceRuntimeOnly {
        extendsFrom(testRuntimeOnly)
    }

    acceptanceCompileOnly {
        extendsFrom(testCompileOnly)
    }

}

idea {
    module {
        testSources.from(
            sourceSets.unit.java.srcDirs,
            sourceSets.integration.java.srcDirs,
            sourceSets.acceptance.java.srcDirs,
        )
    }
}

tasks.register('unit', Test)
tasks.register('integration', Test)
tasks.register('acceptance', Test)
tasks.register('junitTestReport', TestReport)
tasks.register('jacocoUnitTestReport', JacocoReport)
tasks.register('jacocoIntegrationTestReport', JacocoReport)
tasks.register('jacocoAcceptanceTestReport', JacocoReport)

unit {

    useJUnitPlatform()

    setTestClassesDirs(sourceSets.unit.output.classesDirs)
    setClasspath(sourceSets.unit.runtimeClasspath)

    reports {
        html.outputLocation.set(
            file('build/reports/junit/unit')
        )
    }

    finalizedBy(
        jacocoUnitTestReport,
        jacocoTestReport,
        junitTestReport
    )

}

integration {

    useJUnitPlatform()

    setTestClassesDirs(sourceSets.integration.output.classesDirs)
    setClasspath(sourceSets.integration.runtimeClasspath)

    reports {
        html.outputLocation.set(
            file('build/reports/junit/integration')
        )
    }

    finalizedBy(
        jacocoIntegrationTestReport,
        jacocoTestReport,
        junitTestReport
    )

    shouldRunAfter(unit)

}

acceptance {

    useJUnitPlatform()

    setTestClassesDirs(sourceSets.acceptance.output.classesDirs)
    setClasspath(sourceSets.acceptance.runtimeClasspath)

    systemProperty('cucumber.plugin', 'pretty, html:build/reports/cucumber/report.html')

    reports {
        html.outputLocation.set(
            file('build/reports/junit/acceptance')
        )
    }

    testLogging {
        events("STANDARD_OUT")
    }

    testlogger {
        showExceptions(false)
        showPassed(false)
        showSkipped(false)
        showFailed(false)
        showSummary(false)
    }

    finalizedBy(
        jacocoAcceptanceTestReport,
        jacocoTestReport,
        junitTestReport
    )

    shouldRunAfter(integration)

}

test {
    dependsOn(unit, integration, acceptance)
}

jacocoUnitTestReport {

    executionData(unit)
    sourceSets(sourceSets.main)

    reports {
        html.required.set(true)
        html.outputLocation.set(
            file('build/reports/jacoco/unit')
        )
    }

    shouldRunAfter(unit)

}

jacocoIntegrationTestReport {

    executionData(integration)
    sourceSets(sourceSets.main)

    reports {
        html.required.set(true)
        html.outputLocation.set(
            file('build/reports/jacoco/integration')
        )
    }

    shouldRunAfter(integration)

}

jacocoAcceptanceTestReport {

    executionData(acceptance)
    sourceSets(sourceSets.main)

    reports {
        html.required.set(true)
        html.outputLocation.set(
            file('build/reports/jacoco/acceptance')
        )
    }

    shouldRunAfter(acceptance)

}

jacocoTestReport {

    executionData(unit, integration, acceptance)
    sourceSets(sourceSets.main)

    reports {
        html.required.set(true)
        html.outputLocation.set(
            file('build/reports/jacoco/test')
        )
    }

    shouldRunAfter(
        jacocoUnitTestReport,
        jacocoIntegrationTestReport,
        jacocoAcceptanceTestReport
    )

}

junitTestReport {

    destinationDirectory.set(
        file('build/reports/junit/test'),
    )

    testResults.from(
        file('build/test-results/unit/binary'),
        file('build/test-results/integration/binary'),
        file('build/test-results/acceptance/binary')
    )

    shouldRunAfter(unit, integration, acceptance)

}

testlogger {
    theme('mocha')
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom('io.cucumber:cucumber-bom:7.17.0')
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter')

    testImplementation('org.springframework.boot:spring-boot-starter-test')

    acceptanceImplementation('io.cucumber:cucumber-java')
    acceptanceImplementation('io.cucumber:cucumber-spring')
    acceptanceImplementation('io.cucumber:cucumber-junit-platform-engine')
    acceptanceImplementation("org.junit.platform:junit-platform-suite")
}

group = 'sportex'
version = '0.0.1-SNAPSHOT'
